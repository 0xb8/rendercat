#version 450 core

layout(binding=0) uniform sampler2D Source;
layout(binding=0, rgb10_a2ui) restrict writeonly uniform uimage2D outputTexture;

layout(location = 0) uniform vec2 SourceSize;
layout(location = 1) uniform vec2 ViewportSize;
layout(location = 2) uniform vec2 OutputSize;
#define A_GPU 1
#define A_GLSL 1
#include "ffx_a.h"

#define FSR_EASU_F 1

AF4 FsrEasuRF(AF2 p) {
    return textureGather(Source, p, 0);
}
AF4 FsrEasuGF(AF2 p) {
    return textureGather(Source, p, 1);
}
AF4 FsrEasuBF(AF2 p) {
    return textureGather(Source, p, 2);
}

#include "ffx_fsr1.h"
AU4 con0, con1, con2, con3;

void storeGamma(uvec2 pos, vec3 Gamma2Color) {
    uvec3 packed = uvec3(Gamma2Color * 1023.0);
    imageStore(outputTexture, ivec2(pos), uvec4(packed, 3));
}

layout(local_size_x=64, local_size_y=1, local_size_z=1) in;
void main() {
    FsrEasuCon(con0, con1, con2, con3,
        ViewportSize.x, ViewportSize.y,  // Viewport size (top left aligned) in the input image which is to be scaled.
        SourceSize.x, SourceSize.y,  // The size of the input image.
        OutputSize.x, OutputSize.y); // The output resolution.

    AF3 Gamma2Color = AF3(0, 0, 0);
    AU2 gxy = ARmp8x8(gl_LocalInvocationID.x) + AU2(gl_WorkGroupID.x << 4u, gl_WorkGroupID.y << 4u);

    FsrEasuF(Gamma2Color, gxy, con0, con1, con2, con3);
    storeGamma(gxy, Gamma2Color);

    gxy.x += 8u;
    FsrEasuF(Gamma2Color, gxy, con0, con1, con2, con3);
    storeGamma(gxy, Gamma2Color);

    gxy.y += 8u;
    FsrEasuF(Gamma2Color, gxy, con0, con1, con2, con3);
    storeGamma(gxy, Gamma2Color);

    gxy.x -= 8u;
    FsrEasuF(Gamma2Color, gxy, con0, con1, con2, con3);
    storeGamma(gxy, Gamma2Color);
}
