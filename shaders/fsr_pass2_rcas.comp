#version 450 core

layout(binding=0, rgb10_a2ui) readonly uniform uimage2D Source;
layout(binding=1, rgba8) restrict writeonly uniform image2D outputTexture;

layout(location = 0) uniform vec2 fsr_params;
layout(location = 1) uniform ivec2 frame_params;

#define A_GPU 1
#define A_GLSL 1
#include "ffx_a.h"

#define FSR_RCAS_F 1

uint pcg(uint v) {
	uint state = v * uint(747796405) + uint(2891336453);
	uint word = ((state >> ((state >> uint(28)) + uint(4))) ^ state) * uint(277803737);
	return (word >> uint(22)) ^ word;
}


float prng(vec2 uv, float time) {
    return fract(sin(dot(uv + fract(time), vec2(12.9898, 78.233))) * 43758.5453);
}

// pdf: [-0.5;0.5[
// Removes noise modulation effect by reshaping the uniform/rectangular noise
// distribution (RPDF) into a Triangular (TPDF) or Gaussian Probability Density
// Function (GPDF).
// shape = 1.0: Rectangular
// shape = 0.5: Triangular
// shape < 0.5: Gaussian (0.2~0.4)
float pdf(float noise, float shape) {
    float orig = noise * 2.0 - 1.0;
    noise = pow(abs(orig), shape);
    noise *= sign(orig);
    noise -= sign(orig);
    return noise * 0.5;
}



AF4 FsrRcasLoadF(ASU2 p) {
    uvec4 c = imageLoad(Source, p);
    return AF4((c.rgb / 1023.0), c.a / 3.0);
}
void FsrRcasInputF(inout AF1 r, inout AF1 g, inout AF1 b) {}

void storeResult(uvec2 pos, vec3 Gamma2Color) {
    imageStore(outputTexture, ivec2(pos), vec4(Gamma2Color, 1.0));
}

#include "ffx_fsr1.h"
AU4 con0;

void apply_noise(uvec2 pos, inout vec3 Gamma2Color) {
    vec2 uv = vec2(pos) / imageSize(Source);
    const float PDF = 0.3;
    vec3 rgbNoise = vec3(pdf(prng(uv, frame_params[0] * 0.11), PDF),
                         pdf(prng(uv, frame_params[0] * 0.13), PDF),
                         pdf(prng(uv, frame_params[0] * 0.17), PDF));
    FsrLfgaF(Gamma2Color, rgbNoise, fsr_params[1]);
}



layout(local_size_x=64, local_size_y=1, local_size_z=1) in;
void main() {
    FsrRcasCon(con0, fsr_params[0]);

    AF3 Gamma2Color = AF3(0, 0, 0);
    AU2 gxy = ARmp8x8(gl_LocalInvocationID.x) + AU2(gl_WorkGroupID.x << 4u, gl_WorkGroupID.y << 4u);

    FsrRcasF(Gamma2Color.r, Gamma2Color.g, Gamma2Color.b, gxy, con0);
    apply_noise(gxy, Gamma2Color);
    storeResult(gxy, Gamma2Color);
    gxy.x += 8u;

    FsrRcasF(Gamma2Color.r, Gamma2Color.g, Gamma2Color.b, gxy, con0);
    apply_noise(gxy, Gamma2Color);
    storeResult(gxy, Gamma2Color);
    gxy.y += 8u;

    FsrRcasF(Gamma2Color.r, Gamma2Color.g, Gamma2Color.b, gxy, con0);
    apply_noise(gxy, Gamma2Color);
    storeResult(gxy, Gamma2Color);
    gxy.x -= 8u;

    FsrRcasF(Gamma2Color.r, Gamma2Color.g, Gamma2Color.b, gxy, con0);
    apply_noise(gxy, Gamma2Color);
    storeResult(gxy, Gamma2Color);
}
